grammar org.xtext.arithmetic.Arithmetic with org.eclipse.xtext.common.Terminals

import "platform:/resource/emf.arithmetics/model/arithmetics.ecore" as lib
import "http://www.eclipse.org/emf/2002/Ecore" as ecore 


Module returns lib::Module:
	'module' name=ID
	(imports+=Import)*
	(statements+=Statement)*;
	
	

Import returns lib::Import:
	'import' module=[lib::Module];

Statement returns lib::Statement:
	Definition | Evaluation;

Definition returns lib::Definition:
	'def' name=ID ('(' args+=DeclaredParameter (',' args+=DeclaredParameter)* ')')?
	':' expr=Expression ';';

DeclaredParameter returns lib::DeclaredParameter:
	name=ID;

AbstractDefinition returns lib::AbstractDefinition:
	Definition | DeclaredParameter;

Evaluation returns lib::Evaluation:
	expression=Expression ';';

Expression returns lib::Expression:
	Addition;

Addition returns lib::Expression:
	Multiplication (({lib::Plus.left=current} '+' | {lib::Minus.left=current} '-') right=Multiplication)*;

Multiplication returns lib::Expression:
	PrimaryExpression (({lib::Multi.left=current} '*' | {lib::Div.left=current} '/') right=PrimaryExpression)*;

PrimaryExpression returns lib::Expression:
	'(' Expression ')' |
	{lib::NumberLiteral} value=NUMBER |
	{lib::FunctionCall} func=[lib::AbstractDefinition] ('(' args+=Expression (',' args+=Expression)* ')')?;

terminal NUMBER returns ecore::EBigDecimal:
	('0'..'9')* ('.' ('0'..'9')+)?;

@Override
terminal INT returns ecore::EInt:
	'this one has been deactivated';
	